////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////
//
// WE not ME (Bitcoin Indicator)
//
// Telegram: https://t.me/we_not_me
//
// David Stevens <mail.davro@gmail.com>
// 
// Donations are accepted here
// Bitcoin: 3Ph9wP4vgASFbFP9DAabZETycXY66ebA8Y
//
////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////
//@version=4
study("WE-Indicator", "WE", overlay=true)

// Tom Demark
showSellTDs                   = input(true, title="TD Sell")
showBuyTDs                    = input(true, title="TD Buy")
show1to8TDs                   = input(true, title="TD 1-8")
showTDTrendlines              = input(true, title="TD Trend Lines")
showLogGrowthCurves           = input(true, title="Log Growth Curves")
showLogGrowthProjectionCurves = input(true, title="Log Growth Projection Curves")


////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////
//
// RSI Visual Vertical (Green IN, Red OUT) 
//   Red for over bought, out, sell, take, profits, take a short position 
//   Green for oversold, in, buy, maybe take a long position
//
////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////

showVerticalHeatmap = input(false, title="Vertical Heatmap - Show/Hide")

fastMA = ema(high, 12)
slowMA = ema(high, 26)
rsi = if (timeframe.isminutes and timeframe.period == '1')
    rsi(close, 14)
else if (timeframe.isminutes and timeframe.period == '15')
    rsi(close, 14)
else 
    rsi(close, 30)
    
macd = fastMA - slowMA
macdOverExtended = if (timeframe.isdaily)
    100 + (high - macd) / high * 100 <= 191.5 ? 100 + (high - macd) / high * 100 : 0
else if (timeframe.isminutes and timeframe.period == '240') // DONE
    if (rsi > 60)
        100 + (high - macd) / high * 100 <= 196.8 ? 100 + (high - macd) / high * 100 : 0
    else
        100 + (high - macd) / high * 100 >= 203 ? 100 + (high - macd) / high * 100 : 0
else if (timeframe.isminutes and timeframe.period == '15') // DONE
    if (rsi > 60)
        100 + (high - macd) / high * 100 <= 199 ? 100 + (high - macd) / high * 100 : 0
    else
        100 + (high - macd) / high * 100 >= 201 ? 100 + (high - macd) / high * 100 : 0
else if (timeframe.isminutes and timeframe.period == '60') // TODO
    if (rsi > 60)
        100 + (high - macd) / high * 100 <= 198 ? 100 + (high - macd) / high * 100 : 0
    else
        100 + (high - macd) / high * 100 >= 201.8 ? 100 + (high - macd) / high * 100 : 0
else if (timeframe.isminutes and timeframe.period == '1') // DONE
    if (rsi > 60)
        100 + (high - macd) / high * 100 <= 199.8 ? 100 + (high - macd) / high * 100 : 0
    else
        100 + (high - macd) / high * 100 >= 200.5 ? 100 + (high - macd) / high * 100 : 0
else
    0

backgroundColorByRSI = if (showVerticalHeatmap == false)
    na
else if (timeframe.isminutes)

    rsiValueHigh = if (timeframe.period == '1')
        70
    else if (timeframe.period == '5')
        70
    else if (timeframe.period == '15')
        70
    else if (timeframe.period == '30')
        72
    else if (timeframe.period == '60')
        69
    else if (timeframe.period == '240')
        70
    else
        na
    
    rsiValueHigher = if (timeframe.period == '1')
        74
    else if (timeframe.period == '5')
        72
    else if (timeframe.period == '15')
        73
    else if (timeframe.period == '30')
        74
    else if (timeframe.period == '60')
        72
    else if (timeframe.period == '240')
        74  
    else
        na
    
    rsiValueLow = if (timeframe.period == '1')
        30
    else if (timeframe.period == '5')
        35
    else if (timeframe.period == '15')
        32
    else if (timeframe.period == '30')
        37
    else if (timeframe.period == '60')
        34
    else if (timeframe.period == '240')
        42
    else
        na
    
    rsiValueLower = if (timeframe.period == '1')
        27
    else if (timeframe.period == '5')
        33
    else if (timeframe.period == '15')
        30
    else if (timeframe.period == '30')
        32
    else if (timeframe.period == '60')
        32
    else if (timeframe.period == '240')
        40
    else
        na

    if (rsi >= rsiValueHigher and macdOverExtended > 0)
        color.new(color.orange, 40)
    else if (rsi >= rsiValueHigh and macdOverExtended > 0)
        color.new(color.orange, 40)
    else if (rsi >= rsiValueHigher)
        color.new(color.red, 40)
    else if (rsi >= rsiValueHigh)
        color.new(color.red, 70)
    else if (rsi <= rsiValueLower and macdOverExtended > 0)
        color.new(color.blue, 40)
    else if (rsi <= rsiValueLow and macdOverExtended > 0)
        color.new(color.blue, 40)
    else if (rsi <= rsiValueLower)
        color.new(color.green, 40)
    else if (rsi <= rsiValueLow)
        color.new(color.green, 70)
else if (timeframe.isdaily)
    rsiValueHigh = 72
    
    rsiValueHigher = 77  
    
    rsiValueLow = 30
    
    rsiValueLower = 30

    if (rsi >= rsiValueHigher and macdOverExtended > 0)
        color.new(color.orange, 40)
    else if (rsi >= rsiValueHigh and macdOverExtended > 0)
        color.new(color.orange, 40)
    else if (rsi >= rsiValueHigher)
        color.new(color.red, 40)
    else if (rsi >= rsiValueHigh)
        color.new(color.red, 70)
    else if (rsi <= rsiValueLower)
        color.new(color.green, 40)
    else if (rsi <= rsiValueLow)
        color.new(color.green, 70)
else if (timeframe.isweekly)
    rsiValueHigh = 83
    
    rsiValueHigher = 90
    
    rsiValueLow = 44
    
    rsiValueLower = 42

    if (rsi >= rsiValueHigher)
        color.new(color.red, 40)
    else if (rsi >= rsiValueHigh)
        color.new(color.red, 70)
    else if (rsi <= rsiValueLower)
        color.new(color.green, 40)
    else if (rsi <= rsiValueLow)
        color.new(color.green, 70)
else
    na
bgcolor(color=backgroundColorByRSI)


////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////
//
// TD Sequential 
//
////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////
buySignals = 0
buySignals := (close < close[4]) ? buySignals[1] == 9 ? 1 : buySignals[1] + 1 : 0

sellSignals = 0
sellSignals := (close > close[4]) ? sellSignals[1] == 9 ? 1 : sellSignals[1] + 1 : 0

BuyOrSell = max(buySignals, sellSignals)

// Commented out to save on outputs
plotchar(showBuyTDs and show1to8TDs and buySignals and BuyOrSell == 1, char="", text="1", textcolor=color.green, location=location.belowbar, transp=40, title="TD1 Green")
plotchar(showBuyTDs and show1to8TDs and buySignals and BuyOrSell == 2, char="", text="2", textcolor=color.green, location=location.belowbar, transp=40, title="TD2 Green")
plotchar(showBuyTDs and show1to8TDs and buySignals and BuyOrSell == 3, char="", text="3", textcolor=color.green, location=location.belowbar, transp=40, title="TD3 Green")
plotchar(showBuyTDs and show1to8TDs and buySignals and BuyOrSell == 4, char="", text="4", textcolor=color.green, location=location.belowbar, transp=40, title="TD4 Green")
plotchar(showBuyTDs and show1to8TDs and buySignals and BuyOrSell == 5, char="", text="5", textcolor=color.green, location=location.belowbar, transp=40, title="TD5 Green")
plotchar(showBuyTDs and show1to8TDs and buySignals and BuyOrSell == 6, char="", text="6", textcolor=color.green, location=location.belowbar, transp=40, title="TD6 Green")
plotchar(showBuyTDs and show1to8TDs and buySignals and BuyOrSell == 7, char="", text="7", textcolor=color.green, location=location.belowbar, transp=40, title="TD7 Green")
plotchar(showBuyTDs and show1to8TDs and buySignals and BuyOrSell == 8, char="", text="8", textcolor=color.green, location=location.belowbar, transp=20, title="TD8 Green")
plotshape(showBuyTDs and buySignals and BuyOrSell == 9, text="9",color=color.green, textcolor=color.green, size=size.tiny, location=location.belowbar, transp=0, title="TD9 Green")

// Commented out to save on outputs
plotchar(showSellTDs and show1to8TDs and sellSignals and BuyOrSell == 1, char="", text="1", textcolor=color.red, location=location.abovebar, transp=40, title="TD1 Red")
plotchar(showSellTDs and show1to8TDs and sellSignals and BuyOrSell == 2, char="", text="2", textcolor=color.red, location=location.abovebar, transp=40, title="TD2 Red")
plotchar(showSellTDs and show1to8TDs and sellSignals and BuyOrSell == 3, char="", text="3", textcolor=color.red, location=location.abovebar, transp=40, title="TD3 Red")
plotchar(showSellTDs and show1to8TDs and sellSignals and BuyOrSell == 4, char="", text="4", textcolor=color.red, location=location.abovebar, transp=40, title="TD4 Red")
plotchar(showSellTDs and show1to8TDs and sellSignals and BuyOrSell == 5, char="", text="5", textcolor=color.red, location=location.abovebar, transp=40, title="TD5 Red")
plotchar(showSellTDs and show1to8TDs and sellSignals and BuyOrSell == 6, char="", text="6", textcolor=color.red, location=location.abovebar, transp=40, title="TD6 Red")
plotchar(showSellTDs and show1to8TDs and sellSignals and BuyOrSell == 7, char="", text="7", textcolor=color.red, location=location.abovebar, transp=40, title="TD7 Red")
plotchar(showSellTDs and show1to8TDs and sellSignals and BuyOrSell == 8, char="", text="8", textcolor=color.red, location=location.abovebar, transp=20, title="TD8 Red")
plotshape(showSellTDs and sellSignals and BuyOrSell == 9, text="9",color=color.red, textcolor=color.red, size=size.tiny, location=location.abovebar, transp=0, title="TD9 Red")

// TD ALERTS (green)
// alertcondition(showBuyTDs and show1to8TDs and buySignals and BuyOrSell == 3, title="TD3 Green", message='{"text": "TD3 Green", "view": "dev"}' )
// alertcondition(showBuyTDs and show1to8TDs and buySignals and BuyOrSell == 6, title="TD6 Green", message='{"text": "TD6 Green", "view": "dev"}' )
// alertcondition(showBuyTDs and show1to8TDs and buySignals and BuyOrSell == 7, title="TD7 Green", message='{"text": "TD7 Green", "view": "dev"}' )
// alertcondition(showBuyTDs and show1to8TDs and buySignals and BuyOrSell == 8, title="TD8 Green", message='{"text": "TD8 Green", "view": "dev"}' )
alertcondition(showBuyTDs and buySignals and BuyOrSell == 9, title="TD9 Green", message='{"text": "{{ticker}} ({{exchange}} {{interval}}) TD9 Green"}' )

// TD ALERTS (red)
// alertcondition(showSellTDs and show1to8TDs and sellSignals and BuyOrSell == 3, title="TD3 Red", message='{"text": "TD3 Red", "view": "dev"}' )
// alertcondition(showSellTDs and show1to8TDs and sellSignals and BuyOrSell == 6, title="TD6 Red", message='{"text": "TD6 Red", "view": "dev"}' )
// alertcondition(showSellTDs and show1to8TDs and sellSignals and BuyOrSell == 7, title="TD7 Red", message='{"text": "TD7 Red", "view": "dev"}' )
// alertcondition(showSellTDs and show1to8TDs and sellSignals and BuyOrSell == 8, title="TD8 Red", message='{"text": "TD8 Red", "view": "dev"}' )
alertcondition(showSellTDs and sellSignals and BuyOrSell == 9, title="TD9 Red", message='{"text": "{{ticker}} ({{exchange}} {{interval}}) TD9 Red"}' )

////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////
//
// TD Sequential - Trend Lines
//
////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////
highest9 = highest(9)
highTrendLine = 0.0
highTrendLine := buySignals == 9 ? highest9
              : close > highTrendLine[1] ? 0
              : highTrendLine[1]
               
lowest9 = lowest(9)
lowTrendLine = 0.0
lowTrendLine := sellSignals == 9 ? lowest9
              : close < lowTrendLine[1] ? 0
              : lowTrendLine[1]

plot(showTDTrendlines and highTrendLine > 0 ? highTrendLine : na, title="Trendline", color=color.blue, style=plot.style_circles, linewidth=1, transp=0)
plot(showTDTrendlines and lowTrendLine > 0 ? lowTrendLine : na, title="Trendline", color=color.white, style=plot.style_circles, linewidth=1, transp=0)


////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////
//
// Bitcoin Log Growth Curves
//
// Features
//
// RISK Curve
// Curve Projection
//
// Inspired by @Quantadelic aka Pinescript Pleb
//
////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////

highIntercept = input(1.06930947, title="Top Curve Intercept", step=0.001)
highSlope     = input(0.00076, title="Top Curve Slope", step=0.00001)
lowIntercept  = input(-3.0269716, title="Bottom Curve Intercept", step=0.001)
lowSlope      = input(0.001329, title="Bottom Curve Slope", step=0.00001)

// Time Calcs
timeIndex = time < 1279670400000 ? 3.0 : (time - 1279670400000) / 86400000
weight    = (log10(timeIndex + 10) * timeIndex * timeIndex - timeIndex) / 30000
timeDelta = time - time[1]

// Top of Channel Intercept & Slope Calcs
highSlopeCum  = highSlope * timeIndex
highLogDev    = timeIndex > 2 ? log(weight) + highIntercept + highSlopeCum : na
highestLogDev = timeIndex > 2 ? log(weight) + 1.371 + highSlopeCum : na

// Bottom of channel intercept & slope calcs
lowSlopeCum = lowSlope * timeIndex
lowLogDev   = timeIndex > 2 ? log(weight) + lowIntercept + lowSlopeCum : na
lowestLogDev = timeIndex > 2 ? log(weight) + -3.71 + lowSlopeCum : na

// Total channel log range
logRange = highLogDev - lowLogDev

// Fibonacci Levels
fib9098Calc = (logRange * 0.9098) + lowLogDev
fib8541Calc = (logRange * 0.8541) + lowLogDev
fib7639Calc = (logRange * 0.7639) + lowLogDev
fib618Calc  = (logRange * 0.618) + lowLogDev
midCalc     = (logRange * 0.5) + lowLogDev
fib382Calc  = (logRange * 0.382) + lowLogDev
fib2361Calc = (logRange * 0.2361) + lowLogDev
fib1459Calc = (logRange * 0.1459) + lowLogDev
fib0902Calc = (logRange * 0.0902) + lowLogDev

// Scale Log variables to linear
highestDev = if (showLogGrowthCurves)
    pow(2.718281828459, highestLogDev)
    
highDev = if (showLogGrowthCurves)
    pow(2.718281828459, highLogDev)
    
fib9098Dev = if (showLogGrowthCurves)
    pow(2.718281828459, fib9098Calc)
    
fib8541Dev = if (showLogGrowthCurves)
    pow(2.718281828459, fib8541Calc)
    
fib7639Dev = if (showLogGrowthCurves)
    pow(2.718281828459, fib7639Calc)
    
fib618Dev = if (showLogGrowthCurves)
    pow(2.718281828459, fib618Calc)
    
midDev = if (showLogGrowthCurves)
    pow(2.718281828459, midCalc)
    
fib382Dev = if (showLogGrowthCurves)
    pow(2.718281828459, fib382Calc)
    
fib2361Dev = if (showLogGrowthCurves)
    pow(2.718281828459, fib2361Calc)
    
fib1459Dev = if (showLogGrowthCurves)
    pow(2.718281828459, fib1459Calc)
    
fib0902Dev = if (showLogGrowthCurves)
    pow(2.718281828459, fib0902Calc)

lowDev = if (showLogGrowthCurves)
    pow(2.718281828459, lowLogDev)

lowestDev = if (showLogGrowthCurves)
    pow(2.718281828459, lowestLogDev)

// Colour Scheme
colorBullScheme = color(color.red)
colorBearScheme = color(color.green)

// PLOTS - Fibonacci Levels
plot(highestDev, color=colorBullScheme, transp=0, title="RISK 00%")
plot(highDev, color=colorBullScheme, transp=0, title="RISK 100%")
plot(fib9098Dev, color=colorBullScheme, transp=10, title="RISK  90.98%")
plot(fib8541Dev, color=colorBullScheme, transp=20, title="RISK 85.41% ")
plot(fib7639Dev, color=colorBullScheme, transp=20, title="RISK  76.39%")
plot(fib618Dev, color=colorBullScheme, transp=0, title="RISK 61.80%")
plot(midDev, color=color.white, transp=0, title="RISK 50%")
plot(fib382Dev, color=colorBearScheme, transp=0, title="RISK 38.20%")
plot(fib1459Dev, color=colorBearScheme, transp=20, title="RISK 14.59%")
// plot(fib0902Dev, color=colorBearScheme, transp=10, title="RISK 9.02%")
plot(lowDev, color=colorBearScheme, transp=0, title="RISK 0%")
plot(lowestDev, color=colorBearScheme, transp=0, title="RISK 00%")

// Curve extension drawing function and helper variables
Extension(_i, _slope, _intercept) =>
    TI = ((time + timeDelta *_i) -1279670400000) / 86400000
    W = (log10(TI + 10) * TI * TI - TI) / 30000
    HSC = _slope * TI
    HLD = log(W) + _intercept + HSC
    HD = pow(2.718281828459, HLD)
    HD

// Variables for adjusting length of curve extension  relative to chart timeframe
forLoopStep = timeframe.ismonthly == 1 ? ceil( 12 / timeframe.multiplier) : 
              timeframe.isweekly == 1 ? ceil(26 / timeframe.multiplier) : 
              timeframe.isdaily == 1 ? ceil(91 / timeframe.multiplier) : 
              (timeframe.isminutes == 1) and (timeframe.multiplier > 59) ? ceil(10080 / timeframe.multiplier) : na
              
forLoopMax = na(forLoopStep) ? na : forLoopStep * 13

// Mid Curve Slope & Intercept and colours
midSlope                 = (highSlope + lowSlope) * 0.5
midIntercept             = (highIntercept + lowIntercept) * 0.5
colourSchemeProjected    = color(color.blue)
colourSchemeMidProjected = color(color.white)

if (showLogGrowthProjectionCurves and barstate.islast)
    for i = 0 to forLoopMax-1 by forLoopStep
        line.new(time + timeDelta * i, Extension(i, highSlope, highIntercept), time + timeDelta * (i + forLoopStep), Extension(i + forLoopStep, highSlope, highIntercept), xloc=xloc.bar_time, color=colourSchemeProjected)
        line.new(time + timeDelta * i, Extension(i, midSlope, midIntercept), time + timeDelta * (i + forLoopStep), Extension(i + forLoopStep, midSlope, midIntercept), xloc=xloc.bar_time, color=colourSchemeMidProjected)
        line.new(time + timeDelta * i, Extension(i, lowSlope, lowIntercept), time + timeDelta * (i + forLoopStep), Extension(i + forLoopStep, lowSlope, lowIntercept), xloc=xloc.bar_time, color=colourSchemeProjected)
