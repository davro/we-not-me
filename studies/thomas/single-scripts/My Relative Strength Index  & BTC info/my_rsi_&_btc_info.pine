//@version=4
study(title="My Relative Strength Index", shorttitle="MRSI")

///////////////////////////////////////////////////////////
// Input
///////////////////////////////////////////////////////////
var isBtcUsd = syminfo.ticker == "BTCUSD"
var showHTFBtcIndicators = isBtcUsd and timeframe.isdaily or timeframe.isweekly or timeframe.ismonthly

userRsi = input(false, title="Use below RSI")
rsiLength = input(30, minval=1, title="RSI Length")
mode = input("RSI", title="Configuration mode :", options=['RSI', 'Bitcoin Hashrate (daily)', 'Bitcoin NVT (daily)', 'Overflow (All)'])

hideRSI = (mode != "RSI" and mode != 'Overflow (All)')
showNvt = showHTFBtcIndicators and (mode == 'Bitcoin NVT (daily)' or mode == 'Overflow (All)')
showHashrate = showHTFBtcIndicators and (mode == 'Bitcoin Hashrate (daily)')
isOverlow = mode == 'Overflow (All)'

var scaleAdujster = showHashrate ? 58 : 0

///////////////////////////////////////////////////////////
// RSI Calculation
///////////////////////////////////////////////////////////

rsiValue = if (userRsi)
    rsiLength
else if (timeframe.ismonthly or timeframe.isweekly)
    14
else
    30

// Over bought - Over sold values
rsiValueTop_top = if (timeframe.ismonthly)
    92
else if (timeframe.isweekly)
    90
else if (timeframe.isdaily)
    75
else if (timeframe.period == '240')
    72
else if (timeframe.period == '15')
    73
rsiValueTop_bottom = if (timeframe.ismonthly)
    84
else if (timeframe.isweekly)
    83
else if (timeframe.isdaily)
    72
else if (timeframe.period == '240')
    68
else if (timeframe.period == '15')
    70
rsiValueBottom_top = if (timeframe.ismonthly)
    52
else if (timeframe.isweekly)
    48
else if (timeframe.isdaily)
    45
else if (timeframe.period == '240')
    39
else if (timeframe.period == '15')
    32
rsiValueBottom_bottom = if (timeframe.ismonthly)
    49
else if (timeframe.isweekly)
    42
else if (timeframe.isdaily)
    39
else if (timeframe.period == '240')
    35
else if (timeframe.period == '15')
    30


up = rma(max(change(close), 0), rsiValue)
down = rma(-min(change(close), 0), rsiValue)
rsi = (down == 0 ? 100 : up == 0 ? 0 : 100 - (100 / (1 + up / down))) - scaleAdujster

//
//
//

fastMA = ema(high, 12)
slowMA = ema(high, 26)
rsiForColor = if (timeframe.isminutes and timeframe.period == '1')
    rsi(close, 14)
else if (timeframe.isminutes and timeframe.period == '15')
    rsi(close, 14)
else 
    rsi(close, 30)
    
macd = fastMA - slowMA
macdOverExtended = if (timeframe.isweekly)
    if (rsiForColor > 60)
        100 + (high - macd) / high * 100 <= 187.5 ? 100 + (high - macd) / high * 100 : 0
    else
        100 + (high - macd) / high * 100 >= 220 ? 100 + (high - macd) / high * 100 : 0
else if (timeframe.isdaily)
    if (rsiForColor > 60)
        100 + (high - macd) / high * 100 <= 191.5 ? 100 + (high - macd) / high * 100 : 0
    else
        100 + (high - macd) / high * 100 >= 206 ? 100 + (high - macd) / high * 100 : 0
else if (timeframe.isminutes and timeframe.period == '240') // DONE
    if (rsiForColor > 60)
        100 + (high - macd) / high * 100 <= 196.8 ? 100 + (high - macd) / high * 100 : 0
    else
        100 + (high - macd) / high * 100 >= 203 ? 100 + (high - macd) / high * 100 : 0
else if (timeframe.isminutes and timeframe.period == '15') // DONE
    if (rsiForColor > 60)
        100 + (high - macd) / high * 100 <= 199 ? 100 + (high - macd) / high * 100 : 0
    else
        100 + (high - macd) / high * 100 >= 201 ? 100 + (high - macd) / high * 100 : 0
else if (timeframe.isminutes and timeframe.period == '60') // TODO
    if (rsiForColor > 60)
        100 + (high - macd) / high * 100 <= 198 ? 100 + (high - macd) / high * 100 : 0
    else
        100 + (high - macd) / high * 100 >= 201.8 ? 100 + (high - macd) / high * 100 : 0
else if (timeframe.isminutes and timeframe.period == '1') // DONE
    if (rsiForColor > 60)
        100 + (high - macd) / high * 100 <= 199.8 ? 100 + (high - macd) / high * 100 : 0
    else
        100 + (high - macd) / high * 100 >= 200.5 ? 100 + (high - macd) / high * 100 : 0
else
    0

rsiLineColor = color.new(#ff9900, 40)
rsiLineReversalColor = color.new(#be4fea, 0)

rsiValueHigh = if (timeframe.isminutes)
    if (timeframe.period == '30')
        72
    else if (timeframe.period == '60')
        69
    else
        70
else if (timeframe.isdaily)
    72
else if (timeframe.isweekly)
    83
else
    na

rsiValueHigher = if (timeframe.isminutes)
    if (timeframe.period == '1' or timeframe.period == '30' or timeframe.period == '240')
        74
    else if (timeframe.period == '5' or timeframe.period == '60')
        72
    else if (timeframe.period == '15')
        73
    else
        70
else if (timeframe.isdaily)
    77
else if (timeframe.isweekly)
    90
else
    na

rsiValueLow = if (timeframe.isminutes)
    if (timeframe.period == '1')
        30
    else if (timeframe.period == '5')
        35
    else if (timeframe.period == '15')
        32
    else if (timeframe.period == '30')
        37
    else if (timeframe.period == '60')
        34
    else if (timeframe.period == '240')
        42
    else
        30
else if (timeframe.isdaily)
    45
else if (timeframe.isweekly)
    48
else
    na

rsiValueLower = if (timeframe.isminutes)
    if (timeframe.period == '1')
        27
    else if (timeframe.period == '5')
        33
    else if (timeframe.period == '15')
        30
    else if (timeframe.period == '30' or timeframe.period == '60')
        32
    else if (timeframe.period == '240')
        40
    else
        30
else if (timeframe.isdaily)
    39
else if (timeframe.isweekly)
    42
else
    na


if (rsiForColor >= rsiValueHigher and macdOverExtended > 0)
    rsiLineColor := rsiLineReversalColor
else if (rsiForColor >= rsiValueHigh and macdOverExtended > 0)
    rsiLineColor := rsiLineReversalColor
else if (rsiForColor <= rsiValueLower and macdOverExtended > 0)
    rsiLineColor := rsiLineReversalColor
else if (rsiForColor <= rsiValueLow and macdOverExtended > 0)
    rsiLineColor := rsiLineReversalColor

plot(hideRSI == false ? rsi : na, "RSI", color=rsiLineColor, linewidth=2)

top_top = plot(hideRSI == false ? rsiValueTop_top - scaleAdujster : na, style=plot.style_line, color=color.red)
top_bottom = plot(hideRSI == false ? rsiValueTop_bottom - scaleAdujster : na, style=plot.style_line, color=color.red)
bottom_top = plot(hideRSI == false ? rsiValueBottom_top - scaleAdujster : na, style=plot.style_line, color=color.lime)
bottom_bottom = plot(hideRSI == false ? rsiValueBottom_bottom - scaleAdujster : na, style=plot.style_line, color=color.lime)

fill(bottom_bottom, bottom_top, color=color.lime, transp=75)
fill(top_top, top_bottom, color=color.red, transp=75)

///////////////////////////////////////////////////////////
// Hashrate Calculation
///////////////////////////////////////////////////////////

hashrate = security("QUANDL:BCHAIN/HRATE", "D", close, gaps=barmerge.gaps_off, lookahead=barmerge.lookahead_on) / 10000000
hashrate_mean_7 = security("QUANDL:BCHAIN/HRATE", "D", sma(close, 7), gaps=barmerge.gaps_off, lookahead=barmerge.lookahead_on) / 10000000
hashrate_mean_21 = security("QUANDL:BCHAIN/HRATE", "D", sma(close, 21),gaps=barmerge.gaps_off, lookahead=barmerge.lookahead_on) / 10000000
difficulty = security("QUANDL:BCHAIN/DIFF","D", close, gaps=barmerge.gaps_off, lookahead=barmerge.lookahead_on) / 1010000000000

hashrate_plot = plot(showHashrate ? hashrate : na, "Hashrate", style=plot.style_line, color=color.teal)
hashrate_mean_7_plot = plot(showHashrate ? hashrate_mean_7 : na, "Hashrate Mean 7", transp=100, style=plot.style_line)
hashrate_mean_21_plot = plot(showHashrate ? hashrate_mean_21 : na, style=plot.style_line, transp=100, title='Hashrate Mean 21')
difficulty_plot = plot(showHashrate ? difficulty : na, "Difficulty", color=#EAEAEA, style=plot.style_line, linewidth=3)

fill(hashrate_mean_21_plot, hashrate_mean_7_plot, color=hashrate_mean_21 < hashrate_mean_7 ? color.green : color.red, transp=85)

///////////////////////////////////////////////////////////
// NVT Ratio
// Network Value to Transactions Ratio (NVT Ratio) is
// defined as the ratio of market capitalization divided
// by transacted volume in the specified window.
///////////////////////////////////////////////////////////

etrvu = security("QUANDL:BCHAIN/ETRVU", "D", close)
mktcp = security("QUANDL:BCHAIN/MKTCP", "D", close)
nvt_not_medianed = mktcp / etrvu / 2.5 - scaleAdujster
nvtratio = percentile_nearest_rank(nvt_not_medianed, 14, 50)
plot(showNvt ? nvtratio : na, "NVT Ratio", color=#333333, linewidth=2)

ma90 = sma(close, 90)
etrvu_ma90 = security("QUANDL:BCHAIN/ETRVU", "D", ma90)
nvtsRaw = mktcp/etrvu_ma90
nvts = nvtsRaw / 2.5 - scaleAdujster
nvts_rma = rma(nvtsRaw, 90)
nvts_ma_deviation = abs((nvtsRaw - nvts_rma) / nvts_rma)
c_deviation = nvts_ma_deviation > 0.35
plot(showNvt ? nvts : na, "NVT Signal", color=c_deviation ? color.white : color.gray, linewidth=1)

plot(showNvt and isOverlow == false ? 150 / 2.5 : na, "Mid", color=color.olive, style=plot.style_line)
plot(showNvt and isOverlow == false ? 45 / 2.5 : na, "Mid", color=color.olive, style=plot.style_line)

ratio(t1, t2, source) => security(t1, timeframe.period, source) / security(t2, timeframe.period, source)